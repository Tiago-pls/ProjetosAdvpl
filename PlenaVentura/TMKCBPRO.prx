#include "PROTHEUS.CH"
#include "topconn.ch"
#include "tbiconn.ch"
#Include 'Protheus.ch'
#INCLUDE "TOPCONN.CH"

/*-----------------+---------------------------------------------------------+
!Nome              ! TMKCBPRO                                               !
+------------------+---------------------------------------------------------+
!DescriÃ§Ã£o       ! Adiciona uma opção na rotina de CallCenter              !
+------------------+---------------------------------------------------------+
!Autor             ! Tiago Santos                                            !
+------------------+---------------------------------------------------------!
!Data              ! 16/08/2022                                              !
!Data              ! 29/08/2022 - reprocessar Orçamento                      !
+------------------+---------------------------------------------------------!
+------------------+--------------------------------------------------------*/

User Function TMKCBPRO()
Local aButtons := {}
	
	
	Aadd(aButtons,{"OBJETIVO",{||MemCalc()},"Memoria Calculo Preço","Memoria Calculo Preço"})   // Parametros na ordem: Tipo do botao, Procedure, Titulo do Botao
	Aadd(aButtons,{"RepOrc",{||RepOrc()} ,"Reprocessar Orçamento","Reprocessar Orçamento"})   // Parametros na ordem: Tipo do botao, Procedure, Titulo do Botao

Return aButtons

Static Function MemCalc ()

Local oSay1
Local oSay2
Local Sair
Local nTamFonte := 200

Local nCustoMedio := posicione("SB2",1,xFilial("SB2") + GDFIELDGET("UB_PRODUTO",N),"B2_CM1")
Local nMarkup     := u_RetMarkup() 
Local nSomaAliq   := u_RetTotAliq()
Local nPrecoVenda := Round((nCustoMedio + (nCustoMedio * nMarkup /100))  / (1- (nSomaAliq / 100)),2)
Static oDlg

nAliqICMS   := MaFisRet(N,"IT_ALIQICM")
if SA1->A1_GRPTRIB ='200'
    nAliqICMS  := MaFisRet(N,"IT_ALIQSOL") 
Endif
nAliqCofins := IIF(MaFisRet(N,"IT_BASECF2") <> 0, MaFisRet(N,"IT_ALIQCF2"),0)
nAliqPIS    := IIF(MaFisRet(N,"IT_BASEPS2") <> 0, MaFisRet(N,"IT_ALIQPS2"),0)

DEFINE MSDIALOG oDlg TITLE "Memória de cálculo formação do preço de venda" FROM 000, 000 TO 500, 500 COLORS 0, 16777215 PIXEL
@ 010, 040 SAY oSay1 PROMPT "1 - Custo Médio Produto"                                                   SIZE nTamFonte, 007 OF oDlg COLORS 0, 16777215 PIXEL
@ 020, 040 SAY oSay1 PROMPT "2 - Markup (Cliente -> Produto -> Grupo Tributário -> Estado)"             SIZE nTamFonte, 007 OF oDlg COLORS 0, 16777215 PIXEL
@ 030, 040 SAY oSay1 PROMPT "3 - Soma tributos"                                                         SIZE nTamFonte, 007 OF oDlg COLORS 0, 16777215 PIXEL
@ 040, 040 SAY oSay1 PROMPT "4 - Calcula Preço Venda(Custo Médio + Markup  / (1 -(Soma tributos/100)))" SIZE nTamFonte, 007 OF oDlg COLORS 0, 16777215 PIXEL

@ 100, 064 SAY oSay1 PROMPT "Custo Médio Produto: "+transform( nCustoMedio, "@E 999,999.99")             SIZE nTamFonte, 007 OF oDlg COLORS 0, 16777215 PIXEL
@ 110, 064 SAY oSay1 PROMPT "Markup: "  + cValtoChar(nMarkup ) +" %"                               SIZE nTamFonte, 007 OF oDlg COLORS 0, 16777215 PIXEL
@ 120, 064 SAY oSay1 PROMPT "Soma tributos: " + cValtoChar(nSomaAliq ) +" %"                        SIZE nTamFonte, 007 OF oDlg COLORS 0, 16777215 PIXEL
@ 130, 064 SAY oSay1 PROMPT "	ICMS: " + cValtoChar(nAliqICMS ) +" %"                        SIZE nTamFonte, 007 OF oDlg COLORS 0, 16777215 PIXEL
@ 140, 064 SAY oSay1 PROMPT "	PIS: " + cValtoChar(nAliqPIS ) +" %"                        SIZE nTamFonte, 007 OF oDlg COLORS 0, 16777215 PIXEL
@ 150, 064 SAY oSay1 PROMPT "	COFINS: " + cValtoChar(nAliqCofins ) +" %"                        SIZE nTamFonte, 007 OF oDlg COLORS 0, 16777215 PIXEL
@ 170, 064 SAY oSay1 PROMPT "Preço Venda: "  +  transform(  nPrecoVenda , "@E 999,999.99") SIZE nTamFonte, 007 OF oDlg COLORS 0, 16777215 PIXEL

@ 210, 150 BUTTON Sair PROMPT "Sair"                                                   SIZE 037, 012 OF oDlg PIXEL Action oDlg:End()

ACTIVATE MSDIALOG oDlg CENTERED

Return


static function RepOrc
Local nCont :=1
Local nN := N // valor inicial de N
For nCont := 1 to len(aCols)
    N := nCont
    RunTrigger(2,N,nil,,'UB_PRODUTO')
    cTES := GDFIELDGET("UB_TES",N)
    cCFOP := GDFIELDGET("UB_CF",N)
    RunTrigger(2,N,nil,,'UB_OPER')
    GdFieldPut("UB_TES"	  ,cTES 	 ,N,aHeader,aCols)
    GdFieldPut("UB_CF"	  ,cCFOP	 ,N,aHeader,aCols)
Next nCont

N:= nN // retorna o valor de N

msgalert("Orçamento Reprocessado")
return
